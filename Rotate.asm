	org $80
;
LO	.ds 1
HI	.ds 1
PLOTX	.ds 1
PLOTY	.ds 1
YOFSET	.ds 1
HOLD	.ds 1
DRAWX	.ds 1
DRAWY	.ds 1
ACCX	.ds 1
ACCY	.ds 1
DELTAX	.ds 1
DELTAY	.ds 1
INCX	.ds 1
INCY	.ds 1
COUNTR	.ds 1
ENDPT	.ds 1
LOHLD	.ds 1
HIHLD	.ds 1
THETA	.ds 1
SCREEN	.ds 2
PASSV	.ds 2
ACTIVE	.ds 1

COLPF0	= $02C4
COLPF2	= $02C6
COLOR	= $50

; DISPLAY LOCATION
DISP1	= $4010
DISP1A	= DISP1+$0FF0
DISP2	= $6010
DISP2A	= DISP2+$0FF0

;
; DISPLAY LIST
;
; DISPLAY LIST LOCATION
;
	org $2000
;
DLIST	.byte $70,$70,$70,$4E
DLOC	.word DISP1
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$0E,$0E
	.byte $4E
DLOC1	.word DISP1A
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$0E,$0E,$0E,$0E,$0E
	.byte $0E,$0E,$0E,$41
	.word DLIST

; Sine and Cosine tables

COSINE	.byte 127,127,125,121,117,111,103,95,86,75,64,52,40,27,13,0
	.byte -13,-27,-40,-52,-64,-75,-86,-95,-103,-111,-117,-122,-125,-127,-128,-127
	.byte -125,-122,-117,-111,-103,-95,-86,-75,-64,-52,-40,-27,-13,0,13,27
	.byte 40,52,64,75,86,95,104,111,117,122,125,127,127
    
SINE	.byte 0,13,27,40,52,64,75,86,95,104,111
	.byte 117,122,125,127,127,127,125,122,117,111,104,95,86,75,64,52,40,27
	.byte 13,0,-13,-27,-40,-52,-64,-74,-86,-95,-104,-111,-117,-122,-125,-127
	.byte -128,-127,-127,-125,-122,-117,-111,-104,-95,-86,-75,-64,-52,-40,-27,-13,0

;

TEMPX	.ds 100
TEMPY	.ds 100
RX	.ds 100
RY	.ds 100
RZ	.ds 100

NUMPTS	.BYTE 21
DX	.BYTE 20,20,-20,-20,20,20,20,20,-20,-20,-20,-20,-20,20,-20,20,10,-10,-10,-10,10
DY	.BYTE 20,20,20,20,20,-20,-20,20,20,-20,-20,20,-20,-20,-20,-20,-10,-10,10,0,0
DZ	.BYTE 20,-20,-20,20,20,20,-20,-20,-20,-20,20,20,20,20,-20,-20,20,20,20,20,20
TYPE	.BYTE "PDDDDDDDPDDDPDPDPDDPD"


	ORG  $3000
START	CLD 
	LDA #0
	STA $D40E
	STA COLPF2
	STA YTHETA
	STA PTHETA
	STA RTHETA
	STA YFLAG
	STA PFLAG
	STA RFLAG
	LDA # <DLIST
	STA $0230
	LDA # >DLIST
	STA $0231
	LDA #$40
	STA $D40E
	LDA #13
	STA COLPF0
	LDA #1
	STA ACTIVE
	LDA # <DISP1
	STA SCREEN
	LDA # >DISP1
	STA SCREEN+1
	JSR CLS
	LDA # <DISP2
	STA SCREEN
	STA PASSV
	LDA # >DISP2
	STA SCREEN+1
	STA PASSV+1
	JSR CLS
	LDY #0
	LDA #1
	STA COLOR
MAIN	JSR INITR
	LDA CH
	CMP #$FF
	BNE CHKY
	JMP DNCHK
CHKY	CMP #43    ; Y INTERNAL
	BNE CHKP
	LDA #$FF
	EOR YFLAG
	STA YFLAG
	JMP DNCHK
CHKP	CMP #10    ; P INTERNAL
	BNE CHKR
	LDA #$FF
	EOR PFLAG
	STA PFLAG
	JMP DNCHK
CHKR	CMP #40    ; R INTERNAL
	BNE DNCHK
	LDA #$FF
	EOR RFLAG
	STA RFLAG
DNCHK	LDA YFLAG
	BEQ DOPITCH
	INC YTHETA
	LDA YTHETA
	CMP #60
	BNE DOPITCH
	LDA #0
	STA YTHETA
DOPITCH	LDA PFLAG
	BEQ DOROLL
	INC PTHETA
	LDA PTHETA
	CMP #60
	BNE DOROLL
	LDA #0
	STA PTHETA
DOROLL	LDA RFLAG
	BEQ DONEROT
	INC RTHETA
	LDA RTHETA
	CMP #60
	BNE DONEROT
	LDA #0
	STA RTHETA
DONEROT	LDA #255
	STA CH
	JSR YAW
	JSR PITCH
	JSR ROLL
	JSR TRANS
	LDY #0
LPP	JSR PICT
	INY 
	CPY NUMPTS
	BNE LPP
	JSR SWITCH
	LDA PASSV
	STA SCREEN
	LDA PASSV+1
	STA SCREEN+1
	JSR CLS
MEND	JMP MAIN
INITR	TYA 
	PHA 
	LDY #0
IRLP	LDA DX,Y
	STA RX,Y
	LDA DY,Y
	STA RY,Y
	LDA DZ,Y
	STA RZ,Y
	INY 
	CPY NUMPTS
	BNE IRLP
	PLA 
	TAY 
	RTS
	
CH  =   $02FC

YTHETA	.ds 1
PTHETA	.ds 1
RTHETA	.ds 1
YFLAG	.ds 1
PFLAG	.ds 1
RFLAG	.ds 1

ROLL	LDA RTHETA
	TAX
	LDY #0
RLP	LDA RX,Y
	STA ORX
	LDA RY,Y
	STA ORY
	LDA RZ,Y
	STA ORZ
	LDA COSINE,X
	STA MP
	LDA ORX
	STA MC
	JSR MULT
	ASL LOW
	ROL HIGH
	LDA HIGH
	STA RX,Y
	LDA SINE,X
	STA MP
	LDA ORY
	STA MC
	JSR MULT
	ASL LOW
	ROL HIGH
	LDA HIGH
	CLC 
	ADC RX,Y
	STA RX,Y
;  NOW RY
	LDA COSINE,X
	STA MP
	LDA ORY
	STA MC
	JSR MULT
	ASL LOW
	ROL HIGH
	LDA HIGH
	STA RY,Y
	LDA SINE,X
	STA MP
	LDA ORX
	STA MC
	JSR MULT
	ASL LOW
	ROL HIGH
	LDA HIGH
	EOR #$FF
	CLC 
	ADC #1
	CLC 
	ADC RY,Y
	STA RY,Y
	LDA ORZ
	STA RZ,Y
	INY 
	CPY NUMPTS
	BEQ RF
	JMP RLP
RF	RTS

PITCH	LDA PTHETA
	TAX 
	LDY #0
PLP	LDA RX,Y
	STA ORX
	LDA RY,Y
	STA ORY
	LDA RZ,Y
	STA ORZ
	LDA COSINE,X
	STA MP
	LDA ORZ
	STA MC
	JSR MULT
	ASL LOW
	ROL HIGH
	LDA HIGH
	STA RZ,Y
	LDA SINE,X
	STA MP
	LDA ORY
	STA MC
	JSR MULT
	ASL LOW
	ROL HIGH
	LDA HIGH
	CLC 
	ADC RZ,Y
	STA RZ,Y
; NOW RY
	LDA COSINE,X
	STA MP
	LDA ORY
	STA MC
	JSR MULT
	ASL LOW
	ROL HIGH
	LDA HIGH
	STA RY,Y
	LDA SINE,X
	STA MP
	LDA ORZ
	STA MC
	JSR MULT
	ASL LOW
	ROL HIGH
	LDA HIGH
	EOR #$FF
	CLC 
	ADC #1
	CLC 
	ADC RY,Y
	STA RY,Y
	LDA ORX
	STA RX,Y
	INY 
	CPY NUMPTS
	BEQ PF
	JMP PLP
PF	RTS 

YAW	LDA YTHETA
	TAX 
	LDY #0
YLP	LDA RX,Y
	STA ORX
	LDA RY,Y
	STA ORY
	LDA RZ,Y
	STA ORZ
	LDA COSINE,X
	STA MP
	LDA ORX
	STA MC
	JSR MULT
	ASL LOW
	ROL HIGH
	LDA HIGH
	STA RX,Y
	LDA SINE,X
	STA MP
	LDA ORZ
	STA MC
	JSR MULT
	ASL LOW
	ROL HIGH
	LDA HIGH
	CLC 
	ADC RX,Y
	STA RX,Y
; NOW RZ
	LDA COSINE,X
	STA MP
	LDA ORZ
	STA MC
	JSR MULT
	ASL LOW
	ROL HIGH
	LDA HIGH
	STA RZ,Y
	LDA SINE,X
	STA MP
	LDA ORX
	STA MC
	JSR MULT
	ASL LOW
	ROL HIGH
	LDA HIGH
	EOR #$FF
	CLC 
	ADC #1
	CLC 
	ADC RZ,Y
	STA RZ,Y
	LDA ORY
	STA RY,Y
	INY 
	CPY NUMPTS
	BEQ YF
	JMP YLP
YF	RTS

ORX	.ds 1
ORY	.ds 1
ORZ	.ds 1

DRAW	LDA DRAWY
	CMP PLOTY
	BCC YMINUS
	SEC 
	SBC PLOTY
	STA DELTAY
	LDA #1
	STA INCY
	BNE XVEC
YMINUS	LDA PLOTY
	SEC 
	SBC DRAWY
	STA DELTAY
	LDA #255
	STA INCY
XVEC	LDA DRAWX
	CMP PLOTX
	BCC XMINUS
	SEC 
	SBC PLOTX
	STA DELTAX
	LDA #1
	STA INCX
	BNE VECSET
XMINUS	LDA PLOTX
	SEC 
	SBC DRAWX
	STA DELTAX
	LDA #255
	STA INCX
VECSET	LDA #0
	STA ACCY
	STA ACCX
	LDA DELTAX
	CMP DELTAY
	BCC YMAX
	STA COUNTR
	STA ENDPT
	LSR
	STA ACCY
	JMP DRAWGO
YMAX	LDA DELTAY
	STA COUNTR
	STA ENDPT
	LSR
	STA ACCX
DRAWGO	LDA COUNTR
	BEQ DRWEND
BEGIN	LDA ACCY
	CLC 
	ADC DELTAY
	STA ACCY
	CMP ENDPT
	BCC BEGIN2
	LDA ACCY
	SEC 
	SBC ENDPT
	STA ACCY
	LDA PLOTY
	CLC 
	ADC INCY
	STA PLOTY
BEGIN2	LDA ACCX
	CLC 
	ADC DELTAX
	STA ACCX
	CMP ENDPT
	BCC PLOTIT
	LDA ACCX
	SEC 
	SBC ENDPT
	STA ACCX
	LDA PLOTX
	CLC 
	ADC INCX
	STA PLOTX
PLOTIT	JSR PLOT
	DEC COUNTR
	BNE BEGIN
DRWEND	RTS

PICT	TYA 
	PHA 
	TXA 
	PHA 
	LDA TEMPX,Y
	PHA         ; TEMPX ON STACK
	LDA TEMPY,Y
	PHA         ; TEMPY ON STACK
	LDA TYPE,Y
	CMP #"D"
	BEQ ISDRAW
	PLA 
	STA PLOTY
	PLA 
	STA PLOTX
	JSR PLOT
	JMP EPICT
ISDRAW	PLA 
	STA DRAWY
	PLA 
	STA DRAWX
	JSR DRAW
EPICT	PLA 
	TAX 
	PLA 
	TAY 
	RTS

PLOT	LDA PLOTY
	ASL
	STA LO
	LDA #0
	ROL
	STA HI
	ASL LO
	ROL HI
	ASL LO
	LDA LO
	STA LOHLD
	ROL HI
	LDA HI
	STA HIHLD
	ASL LO
	ROL HI
	ASL LO
	ROL HI
	LDA LO
	CLC 
	ADC LOHLD
	STA LO
	LDA HI
	ADC HIHLD
	STA HI
	LDA PASSV
	CLC 
	ADC LO
	STA LO
	LDA PASSV+1
	ADC HI
	STA HI
	LDA PLOTX
	AND #3
	TAX 
	LDA PLOTX
	LSR
	LSR
	STA YOFSET
	TAY 
	LDY COLOR
	LDA BMASK2,X
	AND COLORS,Y
	STA HOLD
	LDA BMASK1,X
	LDY YOFSET
	AND (LO),Y
	ORA HOLD
	STA (LO),Y
PABORT	RTS

COLORS .BYTE $00,$55,$AA,$FF
BMASK1 .BYTE $3F,$CF,$F3,$FC
BMASK2 .BYTE $C0,$30,$0C,$03
COLOR1 .BYTE $40,$10,$04,$01

SWITCH	LDA #0
	STA $D40E
	LDA ACTIVE
	CMP #1
	BNE TWO
	INC ACTIVE
	LDA # <DISP2
	STA DLOC
	LDA # >DISP2
	STA DLOC+1
	LDA # <DISP2A
	STA DLOC1
	LDA # >DISP2A
	STA DLOC1+1
	LDA # <DISP1
	STA PASSV
	LDA # >DISP1
	STA PASSV+1
	JMP SDONE
TWO	DEC ACTIVE
	LDA # <DISP1
	STA DLOC
	LDA # >DISP1
	STA DLOC+1
	LDA # <DISP1A
	STA DLOC1
	LDA # >DISP1A
	STA DLOC1+1
	LDA # <DISP2
	STA PASSV
	LDA # >DISP2
	STA PASSV+1
SDONE	LDA #$40
	STA $D40E
	RTS 

SIN   =   90
TRANS	LDY #0
TLP	LDA RX,Y
	STA MC
	LDA #90
	STA MP
	JSR MULT
	LDA #90
	SEC 
	SBC RZ,Y
	STA DVSOR
	JSR DIV
	LDA QUOT
	CLC 
	ADC #70
	STA TEMPX,Y
	LDA RY,Y
	STA MC
	LDA #90
	STA MP
	JSR MULT
	LDA #90
	SEC 
	SBC RZ,Y
	STA DVSOR
	JSR DIV
	LDA QUOT
	STA MC
	LDA #8
	STA MP
	JSR MULT
	LDA #5
	STA DVSOR
	JSR DIV
	LDA QUOT
	CLC 
	ADC #70
	STA TEMPY,Y
	INY 
	CPY NUMPTS
	BNE TLP
	RTS 


CLS	TYA 
	PHA 
	LDX #30
CLOOP1	LDY #0
	LDA #0
CLOOP2	STA (SCREEN),Y
	INY 
	BNE CLOOP2
	INC SCREEN+1
	DEX 
	BNE CLOOP1
	PLA 
	TAY 
	RTS 

MULT	TXA 
	PHA 
	LDA MP
	EOR MC
	STA MINUS
	LDA MP
	BPL CMC
	EOR #$FF
	CLC 
	ADC #1
	STA MP
CMC LDA MC
	BPL GOMULT
	EOR #$FF
	CLC 
	ADC #1
	STA MC
GOMULT	LDA #0
	STA HIGH
	LDX #8
SHIFT	ASL
	ROL HIGH
	ASL MP
	BCC CHCNT
	CLC 
	ADC MC
	BCC CHCNT
	INC HIGH
CHCNT 	DEX 
	BNE SHIFT
	STA LOW
	LDA MINUS
	BPL FMULT
	LDA LOW
	EOR #$FF
	CLC 
	ADC #1
	STA LOW
	LDA HIGH
	EOR #$FF
	ADC #0
	STA HIGH
FMULT 	PLA 
	TAX 
	RTS


LOW	.ds 1
HIGH	.ds 1
MP	.ds 1
MC	.ds 1
MINUS	.ds 1
	
DIV   	TXA 
	PHA 
	TYA 
	PHA 
	LDA HIGH
	EOR DVSOR
	STA MINUS
	LDA DVSOR
	BPL CKDIV
	EOR #$FF
	CLC 
	ADC #1
	STA DVSOR
CKDIV 	LDA HIGH
	BPL STDIV
	LDA LOW
	EOR #$FF
	CLC 
	ADC #1
	STA LOW
	LDA HIGH
	EOR #$FF
	ADC #0
	STA HIGH
STDIV 	LDX #8
	LDA LOW
	STA QUOT
	LDA HIGH
DIVID 	ASL QUOT
	ROL
	CMP DVSOR
	BCC CKCNT
	SBC DVSOR
	INC QUOT
CKCNT 	DEX 
	BNE DIVID
	STA REMA
	LDA MINUS
	BPL FDIV
	LDA QUOT
	EOR #$FF
	CLC 
	ADC #1
	STA QUOT
FDIV 	PLA 
	TAY 
	PLA 
	TAX 
	RTS
	
DVSOR	.ds 1
QUOT	.ds 1
REMA	.ds 1
;
;
	ORG $02E0
	.word START
	